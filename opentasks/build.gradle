apply plugin: 'com.android.application'

// commit number is only relevant to the application project
def gitCommitNo = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    return Integer.parseInt(stdout.toString().trim())
}

android {
    compileSdkVersion COMPILE_SDK_VERSION.toInteger()
    buildToolsVersion BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "org.dmfs.tasks"
        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        versionCode gitCommitNo() * 10 // spread version code to allow inserting versions if necessary
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard.cfg'
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    productFlavors {
    }

    lintOptions {
        disable 'MissingTranslation' // TODO
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':opentasks-provider')
    implementation project(':opentaskspal')
    implementation 'com.android.support:appcompat-v7:' + SUPPORT_LIBRARY_VERSION
    implementation 'com.android.support:design:' + SUPPORT_LIBRARY_VERSION
    implementation('org.dmfs:android-xml-magic:0.1.1') {
        // xmlpull is part of the runtime, so don't pull it in here
        exclude group: 'xmlpull', module: 'xmlpull'
    }
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    implementation 'com.github.dmfs.color-picker:colorpicker-activity:1.1'
    implementation('au.com.codeka:carrot:2.4.0') {
        exclude module: 'iterators' // TODO Remove when iterators have been removed from codeka:carrot
    }
    implementation('com.github.dmfs.androidcarrot:androidcarrot:13edc04') {
        exclude module: 'carrot'
        exclude group: 'com.android.support'
        exclude module: 'iterators'
        exclude module: 'jems'
    }
    implementation 'org.dmfs:jems:' + JEMS_VERSION
    implementation 'org.dmfs:rfc5545-datetime:' + RFC5545_DATETIME_VERSION
    implementation 'com.github.dmfs.bolts:color-bolts:' + BOLTS_VERSION

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5.1'

    androidTestImplementation('com.android.support.test:runner:' + ANDROID_TEST_RUNNER_VERSION) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test:rules:' + ANDROID_TEST_RUNNER_VERSION) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
